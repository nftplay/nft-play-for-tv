plugins {
    id 'com.android.application'
    id 'com.jakewharton.butterknife'
    id 'org.greenrobot.greendao'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.yindantech.nftplay"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 3
        versionName "1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        rxhttp_rxjava : 'rxjava3',
                        rxhttp_package: 'rxhttp'
                ]
            }
        }
    }

    greendao {
        schemaVersion 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //apk output configuration
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "app_nftplay_${variant.productFlavors[0].name}_${defaultConfig.versionName}.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //Channel configuration
    productFlavors {
        google {
            manifestPlaceholders = [APP_CHANNEL_VALUE: "google"]
        }
        amazon {
            manifestPlaceholders = [APP_CHANNEL_VALUE: "amazon"]
        }
    }
}


dependencies {

//    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.0-rc01'

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //view binding
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    //utils
    implementation 'com.blankj:utilcodex:1.30.6'
    //BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'
    //network
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.ljx.rxhttp:rxhttp:2.5.7'
    annotationProcessor 'com.ljx.rxhttp:rxhttp-compiler:2.5.7'
    //rxjava Lifecycle management
    implementation 'com.ljx.rxlife3:rxlife-rxjava:3.0.0'
    //json parsing
    implementation 'com.ljx.rxhttp:converter-fastjson:2.5.7'
    //rxjava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    //image
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation "com.github.bumptech.glide:okhttp3-integration:4.11.0"
    //event
    implementation 'org.greenrobot:eventbus:3.2.0'
    //database
    implementation 'org.greenrobot:greendao:3.3.0'
    //tv
    implementation "androidx.leanback:leanback:1.0.0"
    implementation 'androidx.leanback:leanback-preference:1.0.0'
    //svg
    implementation 'com.caverock:androidsvg:1.4'
}